/* Simulation of Angular Observations and MLE's */
/* angular.iml                                  */

proc iml;
   Nrepeat = 100;   ttheta = 0.5;    n = 20;       /*ttheta = true theta value */
   MLE = j(Nrepeat, 1, 0);

   do repeat = 1 to Nrepeat;
      ncount = 1;  x = j(n,1,0);
      do while (ncount <= n);
         xtemp = 2*uniform(0)-1;   y = uniform(0);   ytemp = (1+ttheta*xtemp)/2;
         if y <= ytemp then do;  x[ncount] = xtemp;  ncount = ncount + 1;  end;
   end;

   theta = 0;                                    /* user-supplied initial value     */
   maxtol = 0.0001;  maxiter = 10000;

   start ell;
      lik = 0;  lik1 = 0;  lik2 = 0;
      do i=1 to n;
         lik1 = lik1 + x[i]/(1+theta*x[i]);      /* user-supplied first derivative  */
         lik2 = lik2 - (x[i]/(1+theta*x[i]))**2; /* user-supplied second derivative */
      end;
   finish;

   iter = 0;
   tol  = 1;
   do while (iter <= maxiter & tol > maxtol);

      run ell;
      theta1 = theta - lik1/lik2;
      tol = abs(theta1 - theta);
      theta = theta1;
      m = 3*sum(x)/n;
      iter = iter + 1;
   end;

   if theta <= -1 then theta = -1;
      if theta >=  1 then theta = 1;
   MLE[repeat,1] = theta;
   end;

   mean = sum(MLE) / Nrepeat;
   variance = (ssq(MLE) - Nrepeat*mean*mean)/(Nrepeat-1);

   print MLE;
   print Nrepeat n ttheta mean variance;
quit;
