/* Newton-Raphson method for two parameters */
/* mle2.iml                                 */

proc iml;
   theta1 = 1;  theta2 = 8.67;                 /* user-supplied initial values     */
   theta = theta1//theta2;
   maxtol = 0.0001;  maxiter = 10000;

   start ell;
      x={1 1 2 2 3 4 4 5 5 8 8 8 8 11 11 12 12 15 17 22 23};
      n = 21;
      temp0 = exp(theta1*log(x));
      temp1 = exp(theta1*log(x))#log(x);
      temp2 = exp(theta1*log(x))#log(x)#log(x);

      /* user-supplied log-likelihood function */
         lik = n*log(theta1/theta2) + (theta1-1)*sum(log(x)) - sum(temp0)/theta2;

      /* user-supplied first derivatives */
         lik1 = n/theta1 + sum(log(x)) - sum(temp1)/theta2;
         lik2 =-n/theta2 + sum(temp0)/(theta2*theta2);

      /* user-supplied second derivatives */
         lik11 = -n/(theta1*theta1) - sum(temp2)/theta2;
         lik22 = n/(theta2*theta2) - 2*sum(temp0)/(theta2*theta2*theta2);
         lik12 = sum(temp1)/(theta2*theta2);

      /* gradient and Hessian matrix */
      grad = lik1//lik2;
      Hess = (lik11||lik12)//(lik12||lik22);
   finish;

   iter = 0;
   tol  = 1;
   do while (iter <= maxiter & tol > maxtol);
      run ell;
      update = theta - inv(Hess)*grad;
      tol = ssq(update - theta);
      theta = update;
      theta1 = theta[1];
      theta2 = theta[2];
      iter = iter + 1;
   end;

   print iter theta[format=12.4] grad[format=12.4] lik[format=12.4];
quit;
