/* Tukey's Multiple Comparison */
/* tukey.iml                   */

proc iml;
   II = 4;  n = 5;  Nrepeat = 99999;
   Y = j(II,n,0);
   PairDiff = j(II,II,0);
   Q = j(Nrepeat,1,0);   Qsort = j(Nrepeat,1,0);

   do repeat = 1 to Nrepeat;
      do i =1 to II;  do j = 1 to n;
         Y[i,j] = rannor(0);
      end;  end;
      Ybar = Y[,+]/n;
      Residual = Y - Ybar*j(1,n,1);
      s = sqrt(ssq(Residual)/(II*n-II));
      do i = 1 to II;   do j = 1 to II;
         PairDiff[i,j] = abs(Ybar[i] - Ybar[j]);
      end;  end;
      Q[repeat] = max(PairDiff)/s*sqrt(n);
   end;

   R = rank(Q);
   do repeat = 1 to Nrepeat;
      Qsort[R[repeat]] = Q[repeat];
   end;
   Q05 = Qsort[(Nrepeat+1)*0.95];
   print Nrepeat Q05[format=8.3];
quit;
