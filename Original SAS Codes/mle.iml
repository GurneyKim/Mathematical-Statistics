/* Newton-Raphson method for MLE computing */
/* mle.iml                                 */

proc iml;
   theta = 4;                              /* user-supplied initial value      */
   maxtol = 0.0001;  maxiter = 10000;

   start ell;
      lik  = 22*log(theta) - 4*theta;        /* user-supplied log-likelihood     */
      lik1 = 22/theta - 4;                   /* user-supplied first derivative   */
      lik2 = -22/(theta*theta);              /* user-supplied second derivative  */
   finish;

   iter = 0;
   tol  = 1;
   do while (iter <= maxiter & tol > maxtol);

      run ell;
      theta1 = theta - lik1/lik2;
      tol = abs(theta1 - theta);
      theta = theta1;
      iter = iter + 1;
   end;

   print iter theta[format=12.4] lik1[format=12.4] lik[format=12.4];
quit;
