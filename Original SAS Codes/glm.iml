/* Poisson Model Fitting    */
/* File Name : glm.iml      */

proc iml;
    X = {1  2.7,
         1  3.1,
         1  2.0,
         1  2.6,
         1  2.7,
         1  2.5};
    y = {42,  37,  1,  101,  73,  14};

    beta_old = {0, 0};   tol = 1;   repeat=1;

    do while (tol > 0.000001);
       eta = X* beta_old;
       bprime1 = exp(eta);
       bprime2 = bprime1;
       I = X`*diag(bprime2)*X;
       Iinverse = inv(I);
       U = X`*(y-bprime1);
       beta_new = beta_old + Iinverse*X`*(y-bprime1);
       diff = beta_new - beta_old;
       tol = ssq(diff);
       print repeat beta_new[format=8.4];
       repeat = repeat+1;
       beta_old = beta_new;
    end;

   loglik = 2*(eta`*y - sum(exp(eta)));
   se = sqrt(vecdiag(Iinverse));
   print loglik[format=8.4] se[format=8.4];
quit;
