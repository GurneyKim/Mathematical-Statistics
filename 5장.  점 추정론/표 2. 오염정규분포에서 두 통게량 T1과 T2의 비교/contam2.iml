/* Sampling from Contaminated Normal Distributions */
/* contam2.iml                                     */

proc iml;
    theta = 10;  sigma = 1;  n = 40;  n1 = n*0.9;
    Nrepeat = 1000;

    start stats;              /* Computing Median and Mean */
       r = rank(x);  x1 = j(n,1,0);
       do i=1 to n;  x1[r[i]] = x[i];  end;
       if mod(n,2) = 0 then median = (x1[n/2] + x1[n/2+1])/2;
          else median = x1[(n+1)/2];
       mean = sum(x)/n;
    finish;

    do k = 1 to 5;
       x = j(n,1,0);  M1 = j(Nrepeat,1,0);  M2 = j(Nrepeat,1,0);

       /* Monte-Carlo Generation of Contaminated Samples */
       do repeat=1 to Nrepeat;
          do i=1 to n1;    x[i] = theta + sigma*normal(0);    end;
          do i=n1+1 to n;  x[i] = theta + k*sigma*normal(0);  end;
          run stats;
          M1[repeat] = mean;  M2[repeat] = median;
       end;
       mean1 = sum(M1)/Nrepeat;
       var1 = (ssq(M1) - Nrepeat*mean1*mean1)/(Nrepeat-1);
       mean2 = sum(M2)/Nrepeat;
       var2 = (ssq(M2) - Nrepeat*mean2*mean2)/(Nrepeat-1);
       print n k mean1[format=9.2] var1[format=9.4] mean2[format=9.2] var2[format=9.4];
    end;
quit;
